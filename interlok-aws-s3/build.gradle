import org.apache.tools.ant.taskdefs.condition.Os
import org.apache.tools.ant.filters.*

ext {
  componentName='Interlok AWS/S3'
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile "com.amazonaws:aws-java-sdk-s3:$awsSDKVersion"

    compile project(':interlok-aws-common')

    testCompile 'org.mockito:mockito-all:1.10.19'
}

ext.testResourcesDir = { ->
  return "${project.projectDir}/src/test/resources".replaceAll("\\\\", "/")
}

task generateUnitTestProperties {
  doLast {
    ant.property(file: project.projectDir.getCanonicalPath() + "/" + "build.properties")
    ant.property(file:  testResourcesDir() + "/" + propertyTemplate(testResourcesDir(), "unit-tests.properties.template"))
    ant.echoproperties(destfile:  testResourcesDir() + '/unit-tests.properties.resolved', prefix: "junit.")
    ant.replace(file:  testResourcesDir() + '/unit-tests.properties.resolved', token:"junit.", value: "")
  }
}

task copyUnitTestProperties(type: Copy, dependsOn: generateUnitTestProperties) {
  delete file( testResourcesDir() + '/unit-tests.properties')
  from file( testResourcesDir() + "/unit-tests.properties.resolved")
  into (testResourcesDir())
  rename { String fileName ->
    fileName.replace("unit-tests.properties.resolved", 'unit-tests.properties')
  }
  filter(ReplaceTokens, tokens: [JAVA_HOME: System.getProperty("java.home").replaceAll("\\\\", "/")])
  filter(ReplaceTokens, tokens: [BASE_DIR: project.projectDir.getCanonicalPath().replaceAll("\\\\", "/")])
  filter(ReplaceTokens, tokens: [BUILD_DIR: project.buildDir.getCanonicalPath().replaceAll("\\\\", "/")])
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task examplesJar(type: Jar, dependsOn: test) {
    classifier = 'examples'
    from new File(buildDir, '/examples')
}
jar {
  manifest {
    attributes("Built-By": System.getProperty('user.name'),
              "Build-Jdk": System.getProperty('java.version'),
              "Implementation-Title": componentName,
              "Implementation-Version": project.version,
              "Implementation-Vendor-Id": project.group,
              "Implementation-Vendor": organizationName)
  }
}

artifacts {
    archives javadocJar
    archives examplesJar
}

publishing {
  publications {
      mavenJava(MavenPublication) {
          from components.java

          artifact javadocJar { classifier "javadoc" }
          artifact examplesJar { classifier "examples" }
          pom.withXml {
            asNode().appendNode("description", "Integration with AWS S3")
            def properties = asNode().appendNode("properties")
            properties.appendNode("target", "3.3.0+")
            properties.appendNode("tags", "aws,s3")
            properties.appendNode("license", "false")
          }
      }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}

processTestResources.dependsOn copyUnitTestProperties
javadoc.dependsOn offlinePackageList