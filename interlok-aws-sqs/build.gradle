ext {
  componentName='Interlok AWS/SQS'
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile "com.amazonaws:aws-java-sdk-sqs:$awsSDKVersion"
    compile 'com.amazonaws:amazon-sqs-java-messaging-lib:1.0.4'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.6'
    compile 'org.apache.httpcomponents:httpclient:4.5.6'


    compile project(':interlok-aws-common')

    testCompile 'org.mockito:mockito-all:1.10.19'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task examplesJar(type: Jar, dependsOn: test) {
    classifier = 'examples'
    from new File(buildDir, '/examples')
}

artifacts {
    archives javadocJar
    archives examplesJar
}

publishing {
  publications {
      mavenJava(MavenPublication) {
          from components.java

          artifact javadocJar { classifier "javadoc" }
          artifact examplesJar { classifier "examples" }
          pom.withXml {
            asNode().appendNode("description", "Integration with AWS SQS")
            def properties = asNode().appendNode("properties")
            properties.appendNode("target", "3.3.0+")
            properties.appendNode("tags", "aws,sqs")
            properties.appendNode("license", "false")
          }
      }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}
